include: "code.snk"

localrules: mouse_slideseq_get_adata

rule mouse_slideseq_run_notebook:
    input:
        scrna="resources/mouse_sc/scRNAseq.h5ad",
        slideseq="resources/mouse_slideseq/slideseq.h5ad",
        slideseq_by_cluster="resources/mouse_slideseq/slideseq_by_cluster.h5ad",
        slideseq_by_compartment="resources/mouse_slideseq/slideseq_by_compartment.h5ad",
    output:
        collect_figures_inputs('mouse_slideseq'),
        notebook="notebooks/mouse_slideseq.ipynb",
    conda:
        "../envs/TACCO_notebook_env.yml"
    threads:
        8
    resources:
        mem_mb=80000
    log:
        "logs/mouse_slideseq/run_notebook.log"
    benchmark:
        "benchmarks/mouse_slideseq/run_notebook.tsv"
    shell:
        "jupyter nbconvert --to notebook --execute workflow/mouse_slideseq/notebook.ipynb --output ../../{output.notebook}"

rule mouse_slideseq_run_notebook_commot:
    input:
        slideseq="resources/mouse_slideseq/slideseq.h5ad",
        mouse_slideseq_pathway_h5ad="results/mouse_slideseq/data/mouse_slideseq_pathway.h5ad",
    output:
        collect_figures_inputs('mouse_slideseq_commot'),
        notebook="notebooks/mouse_slideseq_commot.ipynb",
    conda:
        "../envs/TACCO_notebook_env.yml"
    threads:
        4
    resources:
        mem_mb=40000
    log:
        "logs/mouse_slideseq/run_notebook_commot.log"
    benchmark:
        "benchmarks/mouse_slideseq/run_notebook_commot.tsv"
    shell:
        "jupyter nbconvert --to notebook --execute workflow/mouse_slideseq/notebook_commot.ipynb --output ../../{output.notebook}"

rule mouse_slideseq_collect_commot:
    input:
        get_mouse_slideseq_collect_commot_inputs
    output:
        mouse_slideseq_pathway_h5ad="results/mouse_slideseq/data/mouse_slideseq_pathway.h5ad",
    conda:
        "../envs/TACCO_notebook_env.yml"
    threads:
        1
    resources:
        mem_mb=40000
    log:
        "logs/mouse_slideseq/collect_commot.log"
    benchmark:
        "benchmarks/mouse_slideseq/collect_commot.tsv"
    script:
        "collect_commot.py"

rule mouse_slideseq_run_commot:
    input:
        slideseq="resources/mouse_slideseq/slideseq.h5ad",
    output:
        commot_sample_h5ad=temp("results/mouse_slideseq/data/commot_per_sample/{sample}.h5ad")
    conda:
        "../envs/commot.yml"
    threads:
        1
    resources:
        mem_mb=40000
    log:
        "logs/mouse_slideseq/run_commot_{sample}.log"
    benchmark:
        "benchmarks/mouse_slideseq/run_commot_{sample}.tsv"
    script:
        "run_commot.py"

rule mouse_slideseq_get_adata:
    output:
        slideseq="resources/mouse_slideseq/slideseq.h5ad",
        slideseq_by_cluster="resources/mouse_slideseq/slideseq_by_cluster.h5ad",
        slideseq_by_compartment="resources/mouse_slideseq/slideseq_by_compartment.h5ad",
    log:
        "logs/mouse_slideseq/mouse_slideseq_get_adata.log"
    benchmark:
        "benchmarks/mouse_slideseq/mouse_slideseq_get_adata.tsv"
    shell:
        '(echo "Loading data from https://singlecell.broadinstitute.org/single_cell/study/SCP1891 . In case of errors, log into the Single Cell Portal, download the files slideseq.h5ad, slideseq_by_cluster.h5ad, and slideseq_by_compartment.h5ad manually, and place them in the resources/mouse_slideseq/ folder." && '
        'mkdir -p resources/mouse_slideseq/ && '
        'pushd resources/mouse_slideseq/ && '
        'gsutil cp gs://fc-bc2c0991-f3b4-4ecd-9f98-b71a6d4c9ca3/slideseq.h5ad . && '
        'gsutil cp gs://fc-bc2c0991-f3b4-4ecd-9f98-b71a6d4c9ca3/slideseq_by_cluster.h5ad . && '
        'gsutil cp gs://fc-bc2c0991-f3b4-4ecd-9f98-b71a6d4c9ca3/slideseq_by_compartment.h5ad .) &> {log}'
