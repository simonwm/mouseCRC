include: "code.snk"

localrules: tcga_download_count_data

rule tcga_run_notebook:
    input:
        scrna="resources/mouse_sc/scRNAseq.h5ad",
        slideseq_by_compartment="resources/mouse_slideseq/slideseq_by_compartment.h5ad",
        h5ad="results/tcga/data/pancanatlas.h5ad",
    output:
        collect_figures_inputs('tcga'),
        notebook="notebooks/tcga.ipynb",
    conda:
        "../envs/TACCO_notebook_env.yml"
    threads:
        8
    resources:
        mem_mb=80000
    log:
        "logs/tcga/run_notebook.log"
    benchmark:
        "benchmarks/tcga/run_notebook.tsv"
    shell:
        "jupyter nbconvert --to notebook --execute workflow/tcga/notebook.ipynb --output ../../{output.notebook}"

rule tcga_make_count_adata:
    input:
        gex="resources/tcga/EBPlusPlusAdjustPANCAN_IlluminaHiSeq_RNASeqV2.geneExp.tsv",
        anno="resources/tcga/TCGA-CDR-SupplementalTableS1.xlsx",
        coadread_anno="resources/tcga/TCGA.COADREAD_clinicalMatrix.tsv",
    output:
        h5ad="results/tcga/data/pancanatlas.h5ad",
    conda:
        "../envs/TACCO_notebook_env.yml"
    log:
        "logs/tcga/tcga_make_count_adata.log"
    benchmark:
        "benchmarks/tcga/tcga_make_count_adata.tsv"
    script:
        "make_count_adata.py"

rule tcga_download_count_data:
    output:
        "resources/tcga/TCGA.COADREAD_clinicalMatrix.tsv",
        "resources/tcga/TCGA-CDR-SupplementalTableS1.xlsx",
        "resources/tcga/EBPlusPlusAdjustPANCAN_IlluminaHiSeq_RNASeqV2.geneExp.tsv",
    log:
        "logs/tcga/tcga_download_count_data.log"
    benchmark:
        "benchmarks/tcga/tcga_download_count_data.tsv"
    shell:
        '(mkdir -p resources/tcga/ && '
        'pushd resources/tcga/ && '
        'wget https://tcga-xena-hub.s3.us-east-1.amazonaws.com/download/TCGA.COADREAD.sampleMap%2FCOADREAD_clinicalMatrix -O TCGA.COADREAD_clinicalMatrix.tsv && '
        'wget https://api.gdc.cancer.gov/data/1b5f413e-a8d1-4d10-92eb-7c4ae739ed81 -O TCGA-CDR-SupplementalTableS1.xlsx && '
        'wget http://api.gdc.cancer.gov/data/3586c0da-64d0-4b74-a449-5ff4d9136611 -O EBPlusPlusAdjustPANCAN_IlluminaHiSeq_RNASeqV2.geneExp.tsv) &> {log}'
